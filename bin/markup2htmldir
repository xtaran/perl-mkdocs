#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use File::Find;
use Text::Markup;
use File::Slurp::Tiny qw(read_file write_file);

die "Usage: $0 <inputdir> <outputdir>" unless @ARGV == 2;
my ($inputdir, $outputdir) = @ARGV;

my $parser = Text::Markup->new(
    default_format   => 'markdown',
    default_encoding => 'UTF-8',
);

my @files;
find(\&_wanted, $inputdir);

my %title;
foreach my $file (@files) {
    my $html = $parser->parse(file => $file);
    if ($html =~ m(<h1[^>]*>(.*?)</h1>)i) {
        $title{$file} = $1;
    } elsif ($html =~ m(<title[^>]*>(.*?)</title>)i) {
        $title{$file} = $1;
    }
}

my $head = qq(<html>\n<head>\n<title>);
my $toc =
    qq(</title>\n</head>\n\n<body>\n<div id="menu">\n).
    generate_toc().
    qq(</div>\n<div id="contents">\n);
my $footer = qq(</div>\n</body>\n</html>\n);

foreach my $file (@files) {
    # Compute output file name.
    my $outputfile = $file;
    $outputfile =~ s:$inputdir:$outputdir:;
    $outputfile =~ s/\.[^.]+$/.html/;

    my $html = $parser->parse(file => $file);

    # Strip off Text::Markup's scaffold and add our own.
    $html =~ s:^.*<body[^>]*>::s;
    $html =~ s:</body>.*$::s;
    $html = $head.$title{$file}.$toc.$html.$footer;

    write_file($outputfile, $html);
}

#use Data::Dumper;
#die Dumper \@files, \%title;

### Functions

sub _wanted {
    return if -d $_;
    my $type = $parser->guess_format($File::Find::name);
    if ($type) {
        push(@files, $File::Find::name);
    }
    return;
}

sub generate_toc {
    my $toc = "<ul>\n";
    foreach my $file (sort keys %title) {
        my $linkfile = $file;
        $linkfile =~ s:$inputdir/::;
        $linkfile =~ s/\.[^.]+$/.html/;
        $toc .= qq(<li><a href="$linkfile">$title{$file}</a></li>\n);
    }
    $toc .= "</ul>\n";
    return $toc;
}
